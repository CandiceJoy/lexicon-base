
////////////////////////////////////////////////////////////////////////////////
// Guns
////////////////////////////////////////////////////////////////////////////////


ACTOR Mayhem17Fist : Fist
{
	Weapon.SlotNumber 1
	States
	{
		Fire:
			PUNG B 4
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
		Normal:
			PUNG C 4 A_CustomPunch(2 * random(1, 10), TRUE, 0, "Mayhem17Puff")
			Goto FireEnd
		Berserked:
			PUNG C 4 A_CustomPunch(20 * random(1, 10), TRUE, 0, "Mayhem17Puff")
		FireEnd:
			PUNG D 5
			PUNG C 4
			PUNG B 5 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17Chainsaw : Chainsaw
{
	Weapon.SlotNumber 1
	States
	{
		Fire:
			SAWG AB 4 A_Saw("weapons/sawfull", "weapons/sawhit", 2, "Mayhem17Puff")
			SAWG B 0 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17Pistol : Pistol
{
	Weapon.SlotNumber 2
	States
	{

		Fire:
			PISG A 4
			PISG B 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
			PISG B 0 A_GunFlash
			PISG B 6 A_FireBullets (5.6, 0, 1, 5, "Mayhem17Puff")
			PISG C 4
			PISG B 5 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17Shotgun : Shotgun
{
	Weapon.SlotNumber 3
	States
	{
		Fire:
			SHTG A 3
			SHTG A 0 A_FireBullets (5.6, 0, 7, 5, "Mayhem17Puff")
			SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
			SHTG A 7 A_GunFlash
			SHTG BC 5
			SHTG D 4
			SHTG CB 5
			SHTG A 3
			SHTG A 7 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17SuperShotgun : SuperShotgun
{
	Weapon.SlotNumber 3
	States
	{
		Fire:
			SHT2 A 3
			SHT2 A 0 A_FireBullets (11.2, 7.1, 20, 5, "Mayhem17Puff")
			SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
			SHT2 A 7 A_GunFlash
			SHT2 B 7
			SHT2 C 7 A_CheckReload
			SHT2 D 7 A_OpenShotgun2
			SHT2 E 7
			SHT2 F 7 A_LoadShotgun2
			SHT2 G 6
			SHT2 H 6 A_CloseShotgun2
			SHT2 A 5 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17Chaingun : Chaingun
{
	Weapon.SlotNumber 4
	States
	{
		Fire:
			CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
			CHGG A 0 A_GunFlash
			CHGG A 4 A_FireBullets(5.6, 0, 1, 5, "Mayhem17Puff")
			CHGG B 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
			CHGG B 0 A_GunFlash("Flash2")
			CHGG B 4 A_FireBullets(5.6, 0, 1, 5, "Mayhem17Puff")
			CHGG B 0 A_ReFire
			Goto Ready
		Flash:
			CHGF A 4 Bright A_Light1
			Goto LightDone
		Flash2:
			CHGF B 4 Bright A_Light2
			Goto LightDone
	}
}

actor Mayhem17RocketLauncher : RocketLauncher
{
	Weapon.SlotNumber 5
	States
	{
		Fire:
			MISG B 8 A_GunFlash
			MISG B 12 A_FireCustomMissile("Mayhem17BulletBill")
			MISG B 0 A_ReFire
			Goto Ready
	}
}

ACTOR Mayhem17PlasmaRifle : PlasmaRifle
{
	Weapon.SlotNumber 6
	States
	{
		Fire:
			PLSG A 0 A_Jump(127, "Fire2")
			PLSG A 0 A_GunFlash
			PLSG A 3 A_FireCustomMissile("Mayhem17FireBall")
			PLSG B 20 A_ReFire
			Goto Ready

		Fire2:
			PLSG A 0 A_GunFlash("Flash2")
			PLSG A 3 A_FireCustomMissile("Mayhem17FireBall")
			PLSG B 20 A_ReFire
			Goto Ready

		Flash:
			M737 A 4 Bright A_Light1
			Goto LightDone

		Flash2:
			M737 B 4 Bright A_Light1
			Goto LightDone
	}
}
////////////////////////////////////////////////////////////////////////////////
// Projectiles
////////////////////////////////////////////////////////////////////////////////

ACTOR Mayhem17BaronFireBall : BaronBall
{
	States
	{
		Spawn:
			M740 AB 4 Bright
			Loop
		Death:
			M738 CDEFG 6 Bright
			Stop
	}
}

ACTOR Mayhem17FireBall : PlasmaBall
{
	States
	{
		Spawn:
			M738 AB 6 Bright
			Loop
		Death:
			M738 CDEFG 4 Bright
			Stop
	}
}

ACTOR Mayhem17BulletBill : Rocket
{
	States
	{
		Spawn:
			M735 A 1 Bright
			Loop
	}
}


ACTOR Mayhem17GreenFireBall : ArachnotronPlasma
{
	States
	{
		Spawn:
			M734 AB 5 Bright
			Loop
		Death:
			M734 CDEFG 5 Bright
			Stop
	}
}

ACTOR Mayhem17Puff : BulletPuff
{
	States
	{
		Spawn:
			M739 A 4 Bright
			M739 B 4
		Melee:
			M739 CD 4
			Stop
	}
}

////////////////////////////////////////////////////////////////////////////////
// Enemies
////////////////////////////////////////////////////////////////////////////////

ACTOR Mayhem17BaronOfHell : BaronOfHell
{
	States
	{
		Melee:
		Missile:
			BOSS EF 8 A_FaceTarget
			BOSS G 8 A_CustomComboAttack("Mayhem17BaronFireBall", 32, 10 * random(1, 8), "baron/melee")
			Goto See
	}
}

ACTOR Mayhem17HellKnight : HellKnight
{
	States
	{
		Melee:
		Missile:
			BOS2 EF 8 A_FaceTarget
			BOS2 G 8 A_CustomComboAttack("Mayhem17BaronFireBall", 32, 10 * random(1, 8), "baron/melee")
			Goto See
	}
}

ACTOR Mayhem17Cyberdemon : Cyberdemon
{
	States
	{
		Missile:
			CYBR E 6 A_FaceTarget
			CYBR F 12 A_CustomMissile("Mayhem17BulletBill")
			CYBR E 12 A_FaceTarget
			CYBR F 12 A_CustomMissile("Mayhem17BulletBill")
			CYBR E 12 A_FaceTarget
			CYBR F 12 A_CustomMissile("Mayhem17BulletBill")
			Goto See
	}
}

ACTOR Mayhem17Arachnotron : Arachnotron
{
	States
	{
		Missile:
			BSPI A 20 Bright A_FaceTarget
			BSPI G 4 Bright A_CustomMissile("Mayhem17GreenFireBall", 20)
			BSPI H 4 Bright
			BSPI H 1 Bright A_SpidRefire
			Goto Missile+1
	}
}

Actor Mayhem17TurtleShell : WolfensteinSS
{
	Health 999999
	Speed 25
	Radius 30
	Height 64
	PainChance 0
	Mass 300
	+NOBLOOD
	-COUNTKILL
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	States
	{
	Spawn:
		M744 A 1 A_Look
		goto Spawn
	See:
		M744 ABCDEFGH 2 A_Chase
		goto See
	Pain:
		TROO A -1
		stop
	Melee:
		M744 A 0 A_SkelFist
		M744 A 1 A_SkelFist
		M744 ABCDEFGH 2 A_Chase
		goto See
	Missile:
		TROO A -1
		stop
	Death:
		M744 A 10 Bright A_PainAttack
		goto Death
	XDeath:
		TROO A -1
		stop
	Raise:
		TROO A -1
		stop
	}
}

Actor Mayhem17Bowser
{
    Health 8000
    Speed 16
    Radius 48
    Height 96
    PainChance 5
    Mass 1000
    +COUNTKILL
    +SOLID
    +SHOOTABLE
    +NOICEDEATH
    SeeSound "mayhem17/bowssight"
    AttackSound "mayhem17/bowsattack"
    PainSound "mayhem17/bowspain"
    DeathSound "mayhem17/bowsdeath"
    Obituary "%o fell before King Koopa's might."
    States
    {
    Spawn:
        M743 A 10 A_Look
        Loop
    See:
        M743 ABCDEFGH 3 A_Chase
        Loop
    Pain:
        M743 Q 8 A_Pain
        Goto See
    Melee:
        M743 L 6 A_SkelWhoosh
        M743 MN 6 A_FaceTarget
        M743 O 0 A_SkelFist
        M743 P 6 A_SkelFist
    Missile:
        M743 IJ 6 A_FaceTarget
        M743 K 6 Bright A_FatAttack1
        M743 K 6 Bright A_FatAttack2
        M743 K 6 Bright A_FatAttack3
        M743 K 1 A_SpidRefire
        Goto Missile+2
    Death:
        M743 RSTUV 8
        M743 W 8 A_KeenDie
        M743 X -1
        Stop
    }
}

Actor MayhemBowserCoop : MayhemBowser
{
    Health 12000
}

////////////////////////////////////////////////////////////////////////////////
// Items
////////////////////////////////////////////////////////////////////////////////

Actor Mayhem17RocketAmmo : RocketAmmo
{
	Inventory.PickupMessage "Bullet Bill!"
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M736 A -1
			Stop
	}
}

Actor Mayhem17RedMushroom : Soulsphere
{
	Inventory.PickupMessage "Super shroom!" // "Supercharge!"
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M700 ABCDCB 6 Bright
			Loop
	}
}

Actor Mayhem17YellowMushroom : Megasphere
{
	Inventory.PickupMessage "Mega shroom!" // "MegaSphere!"
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M701 ABCD 6 Bright
			Loop
	}
}

Actor Mayhem17Star : InvulnerabilitySphere
{
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M702 ABCD 6 Bright
			Loop
	}
}

Actor Mayhem17Berserk : Berserk
{
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M703 A -1 Bright
			Stop
  }
}

Actor Mayhem17RadSuit : RadSuit
{
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M704 A -1 Bright
			Stop
	}
}

Actor Mayhem17BlurSphere : BlurSphere
{
	Inventory.PickupSound "mayhem17/p_pkup"
	States
	{
		Spawn:
			M705 ABCD 6 Bright
			Loop
	}
}

Actor Mayhem17BlueCoin : HealthBonus
{
	Inventory.PickupMessage "Picked up a health coin."
	Inventory.PickupSound "mayhem17/i_pkup"
	States
	{
		Spawn:
			M706 ABCDCB 6
			Loop
	}
}

Actor Mayhem17GreenCoin : ArmorBonus
{
	Inventory.PickupMessage "Picked up an armor coin." // "Picked up an armor bonus."
	Inventory.PickupSound "mayhem17/i_pkup"
	States
	{
		Spawn:
			M707 ABCDCB 6
			Loop
	}
}

Actor Mayhem17RedCoin : Inventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.NEVERRESPAWN
	Inventory.MaxAmount 0
	Inventory.PickupMessage "A mysterious red coin..."
	Inventory.PickupSound "mayhem17/i_pkup"
	Inventory.UseSound ""
	States
	{
		Spawn:
			M708 A 10
			M708 B 10 Bright
			Loop
	}
}

Actor Mayhem17GreenArmor : GreenArmor
{
	States
	{
		Spawn:
			M709 A 6
			M709 B 6 Bright
			Loop
	}
}

Actor Mayhem17BlueArmor : BlueArmor
{
	States
	{
		Spawn:
			M710 A 6
			M710 B 6 Bright
			Loop
	}
}

Actor Mayhem17YellowKey : CustomInventory
{
	Inventory.PickupMessage "Picked up a yellow key."
	Inventory.PickupSound "mayhem17/i_pkup"
	+COUNTITEM
	States
	{
		Spawn:
			M711 A 10
			M711 B 10 Bright
			Loop
		Pickup:
			TNT1 A 0 ACS_NamedExecute("SharedKey_Pickup", 0, 1, 1)
			Stop
	}
}

Actor Mayhem17BlueKey : CustomInventory
{
	Inventory.PickupMessage "Picked up a blue key."
	Inventory.PickupSound "mayhem17/i_pkup"
	+COUNTITEM
	States
	{
		Spawn:
			M712 A 10
			M712 B 10 Bright
			Loop
		Pickup:
			TNT1 A 0 ACS_NamedExecute("SharedKey_Pickup", 0, 2, 1)
			Stop
	}
}

Actor Mayhem17RedKey : CustomInventory
{
	Inventory.PickupMessage "Picked up a red key."
	Inventory.PickupSound "mayhem17/i_pkup"
	+COUNTITEM
	States
	{
		Spawn:
			M713 A 10
			M713 B 10 Bright
			Loop
		Pickup:
			TNT1 A 0 ACS_NamedExecute("SharedKey_Pickup", 0, 0, 1)
			Stop
	}
}

////////////////////////////////////////////////////////////////////////////////
// Decoration
////////////////////////////////////////////////////////////////////////////////

Actor Mayhem17TallBush : BigTree
{
	States
	{
		Spawn:
			M714 A -1
			Stop
	}
}

Actor Mayhem17TinyBush : TorchTree
{
	States
	{
		Spawn:
			M715 A -1
			Stop
	}
}

Actor Mayhem17Rock : Stalagtite
{
	States
	{
		Spawn:
			M716 A -1
			Stop
	}
}

Actor Mayhem17Plant : BrainStem
{
	States
	{
		Spawn:
			M717 A -1
			Stop
	}
}

Actor Mayhem17Pokey : EvilEye
{
	States
	{
		Spawn:
			M718 ABCB 6 Bright
			Loop
	}
}

Actor Mayhem17RedMushroomTree : TallGreenColumn
{
	States
	{
		Spawn:
			M719 A -1
			Stop
	}
}

Actor Mayhem17GreenMushroomTree : ShortGreenColumn
{
	States
	{
		Spawn:
			M720 A -1
			Stop
	}
}

Actor Mayhem17TallBushSnowy : TallRedColumn
{
	States
	{
		Spawn:
			M721 A -1
			Stop
	}
}

Actor Mayhem17Vase : ShortRedColumn
{
	States
	{
		Spawn:
			M722 A -1
			Stop
	}
}

Actor Mayhem17PalmTree : HeartColumn
{
	States
	{
		Spawn:
			M723 AB 14
			Loop
	}
}

Actor Mayhem17TinyBushSnowy : SkullColumn
{
	States
	{
		Spawn:
			M724 A -1
			Stop
	}
}

Actor Mayhem17StonePillar : TechPillar
{
	States
	{
		Spawn:
			M725 A -1
			Stop
	}
}

Actor Mayhem17Vines : ColonGibs
{
	States
	{
		Spawn:
			M726 A -1
			Stop
	}
}

Actor Mayhem17Cactus : DeadStick
{
	States
	{
		Spawn:
			M727 A -1
			Stop
	}
}

Actor Mayhem17QuestionSphere : HeadsOnAStick
{
	States
	{
		Spawn:
			M728 A -1
			Stop
	}
}

Actor Mayhem17BowserPillar : HeadOnAStick
{
	States
	{
		Spawn:
			M729 A -1
			Stop
	}
}

Actor Mayhem17Muncher : LiveStick
{
	States
	{
		Spawn:
			M730 A 6
			M730 B 8
			Loop
	}
}

Actor Mayhem17HangingLantern : BloodyTwitch
{
	States
	{
		Spawn:
			M731 A 10
			M731 B 15
			M731 C 8
			M731 B 6
			Loop
	}
}

Actor Mayhem17TallTorchPillar : RedTorch
{
	States
	{
		Spawn:
			M732 ABCD 4 Bright
			Loop
	}
}

Actor Mayhem17SmallTorchPillar : ShortRedTorch
{
	States
	{
		Spawn:
			M733 ABCD 4 Bright
			Loop
	}
}

Actor Mayhem17BlueTorch : BlueTorch
{
	States
	{
		Spawn:
			M741 ABCD 4 Bright
			Loop
	}
}

Actor Mayhem17ShortBlueTorch : ShortBlueTorch
{
	States
	{
		Spawn:
			M742 ABCD 4 Bright
			Loop
	}
}